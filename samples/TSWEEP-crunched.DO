1 CLEAR:VE$="2.6.0":CLS
2 REM
3 REM
10 DATA30,00,70,35,78,35,120,22,1,146,139,119,35,120,22,225,130,120,60,155,119,35,121,22,1,146,139,119,35,121,22,249,130,121,60,155,119,201:'
11 DIMXY%(21):A%=0:FORN=0TO37:READA%:POKEVARPTR(XY%(3))+N,A%:NEXTN
20 DATA1,0,0,17,8,32,112,35,113,35,4,120,186,218,6,8,6,0,12,121,187,218,6,8,201:'
21 DIMMA%(255):DIMMG%(12):FORN=0TO24:READA%:POKEVARPTR(MG%(0))+N,A%:NEXTN
30 DATA0,17,0,32,213,30,255,71,87,62,1,78,35,35,129,122,206,0,87,12,121,198,255,123,206,0,95,184,122,194,45,8,225,114,201:'
31 DIMTS%(18):FORN=0TO34:READA%:POKEVARPTR(TS%(0))+N,A%:NEXTN
100 YO%=40:IFPEEK(1)<>171ANDPEEK(63035)=8THENLD%=0:H$="  [H]elp":GOTO900
110 IF(PEEK(1)<>171ANDPEEK(63035)=25)OR(PEEK(1)=171ANDPEEK(61190)=25)THENPO%=640ELSEPO%=320
115 IF(PEEK(1)<>171ANDPEEK(63036)=80)OR(PEEK(1)=171ANDPEEK(61191)=80)THENYO%=80:PO%=1280
120 LD%=1:H$="        
900 GOSUB24000:GOTO14000
2915 GOSUB17300'
3000 FORN=0TO50
3005 KEY(8)ON:IFN=0THENPRINT@Y*YO%+X,CN$;ELSEIFN=25THENPRINT@Y*YO%+X,CF$;ELSEPRINT"";
3010 IFPEEK(65339)<>72ANDLD%<>1THENPRINTCHR$(27)+"U":GOSUB17100
3020 ONINSTR(1,S$,INKEY$)GOTO3030,3500,3520,3540,3560,3600,3600,4000,4000,3560,3560,3520,3520,3540,3540,3500,3500,21000,21000
3030 NEXTN:GOTO3000
3200 IFMC%=0THENME$(1)="    WIN!":GOTO12200ELSE2915
3500 IFX<31THENX=X+1:GOTO3000ELSE3000
3520 IFX>0THENX=X-1:GOTO3000ELSE3000
3540 IFY<7THENY=Y+1:GOTO3000ELSE3000
3560 IFY>0THENY=Y-1:GOTO3000ELSE3000
3600 IFS%(X,Y,1)=0THENS%(X,Y,1)=1:PRINTCHR$(255);:GOTO3000
3610 IFS%(X,Y,1)=1THENS%(X,Y,1)=0:PRINTCHR$(239);:GOTO3000
3620 GOTO3000
4000 GOSUB24000:IFM=1THEN5000
4001 IFS%(X,Y,1)=1THEN3000
4002 PRINT@3*YO%+9,"PLACING MINES!";
4010 N=0'
4013 MG%(7)=VARPTR(MG%(3)):MG%(11)=VARPTR(MG%(3)):CALLVARPTR(MG%(0)),0,VARPTR(MA%(0))
4015 POKEVARPTR(XY%(0)),X:POKEVARPTR(XY%(0))+1,Y:CALLVARPTR(XY%(3)),0,VARPTR(XY%(0))
4016 J%=0:FORTY%=PEEK(VARPTR(XY%(2)))TOPEEK(VARPTR(XY%(2))+1):FORTX%=PEEK(VARPTR(XY%(1)))TOPEEK(VARPTR(XY%(1))+1)
4017 MA%(32*TY%+TX%)=-1:J%=J%+1
4018 NEXT:NEXT
4020 SE=(-3600*VAL(RIGHT$(TIME$,2)))+(-60*VAL(MID$(TIME$,4,2)))-VAL(RIGHT$(TIME$,2))
4030 DU=RND(SE):DU=RND(1)
4060 FORN%=0TOMN%-1'
4065 I2%=0:I%=FIX(RND(1)*(256-J%)):J%=J%+1
4066 TS%(15)=(VARPTR(TS%(0))+8):TS%(1)=VARPTR(I2%):CALLVARPTR(TS%(0)),I%,VARPTR(MA%(0))
4070 TX%=PEEK(VARPTR(MA%(I2%))):TY%=PEEK(VARPTR(MA%(I2%))+1)
4110 S%(TX%,TY%,0)=1:MI%(N%,0)=TX%:MI%(N%,1)=TY%:MA%(I2%)=-1:NEXT
4200 M=1'
4400 PRINT@3*YO%+9,"CALCULATING...";
4600 FORN%=0TOMN%-1'
4610 CX%=MI%(N%,0):CY%=MI%(N%,1)
4630 POKEVARPTR(XY%(0)),CX%:POKEVARPTR(XY%(0))+1,CY%:CALLVARPTR(XY%(3)),0,VARPTR(XY%(0))
4640 FORTY%=PEEK(VARPTR(XY%(2)))TOPEEK(VARPTR(XY%(2))+1):FORTX%=PEEK(VARPTR(XY%(1)))TOPEEK(VARPTR(XY%(1))+1)
4650 S%(TX%,TY%,2)=S%(TX%,TY%,2)+1
4660 NEXT:NEXT:NEXT
4700 PRINT@3*YO%+9,STRING$(14,239)
5000 IFS%(X,Y,0)=2ANDS%(X,Y,2)>0THEN17000
5010 IFS%(X,Y,1)<>1ANDS%(X,Y,0)<>2THENST%(W%,0)=X:ST%(W%,1)=Y:W%=W%+1:S%(X,Y,1)=2ELSE3000'
5060 CX%=ST%(R%,0):CY%=ST%(R%,1):R%=R%+1'
5070 IFS%(CX%,CY%,0)=1ORS%(CX%,CY%,0)=3THEN12000ELSEMC%=MC%-1:S%(CX%,CY%,0)=2'
6010 C=S%(CX%,CY%,2)
7160 IFC=0THENPRINT@(CY%*YO%)+CX%,CHR$(32);:S%(CX%,CY%,1)=32:GOTO8010
7170 IFC>0THENPRINT@(CY%*YO%)+CX%,RIGHT$(STR$(C),1);:S%(CX%,CY%,1)=ASC(RIGHT$(STR$(C),1))
7300 IFR%=W%THEN3200
7400 IFR%<W%THEN5060
8010 POKEVARPTR(XY%(0)),CX%:POKEVARPTR(XY%(0))+1,CY%
8020 CALLVARPTR(XY%(3)),0,VARPTR(XY%(0))
8050 N=VARPTR(XY%(2)):FORTY%=PEEK(N)TOPEEK(N+1)
8060 N=VARPTR(XY%(1)):FORTX%=PEEK(N)TOPEEK(N+1)
8070 IFS%(TX%,TY%,0)<2THENST%(W%,0)=TX%:ST%(W%,1)=TY%:W%=W%+1:S%(TX%,TY%,0)=S%(TX%,TY%,0)+2
8090 NEXT:NEXT
8160 IFR%<W%GOTO5060ELSE3200
12000 FORC=0TOMN%-1:PRINT@MI%(C,0)+MI%(C,1)*YO%,CHR$(42);:NEXT'
12200 FORN=0TO6:PRINT@(N*YO%)+32,ME$(N);:NEXT'
12270 KEY(8)STOP:GOSUB20000:IFYN%=1THEN1ELSECLS:MENU
14000 CLS:PRINTCHR$(27)+CHR$(85);
14030 PRINT@2*YO%+14,"TEXT SWEEPER":PRINT@3*YO%+13,"BY G.M. RIMAKIS":PRINT@5*YO%+7,"Press [SPACEBAR] to start.
14040 PRINT@7*YO%,"Ver. "+VE$;
14060 S$="Q"+CHR$(32)
14070 ONINSTR(1,S$,INKEY$)GOTO14070,14100
14080 GOTO14070
14100 CLS:S$="Q"+CHR$(28)+CHR$(29)+CHR$(31)+CHR$(30)+CHR$(70)+CHR$(102)+CHR$(32)+CHR$(13)+"WwAaSsDdHh":CN$=CHR$(27)+CHR$(80):CF$=CHR$(27)+CHR$(81)
14110 GOSUB32000:GOSUB24000'
14120 N%=VAL(INKEY$):IFN%>5ORN%<1THEN14120ELSEIFN%=5THEN14250
14130 MN%=ASC(MID$("!17'",N%,1))-3:GOTO15100
14250 CLS:INPUT"Enter number of mines. (10-217)";MN%
14300 IFMN%<10ORMN%>217GOTO14250
15100 MC%=256-MN%
16070 DIMS%(31,7,2)'
16075 DIMST%(271,1)'
16076 DIMMI%(MN%-1,1)'
16077 DIMME$(6)
16078 ME$(0)="     YOU":ME$(1)="   LOSE!":ME$(2)="        ":ME$(3)="    Play":ME$(4)="  Again?":ME$(5)="   (Y/N)":ME$(6)=ME$(2)
16080 CLS:GOSUB17200:FORY=0TO7
16085 PRINT@Y*YO%,STRING$(32,239);
16150 NEXT
16180 X=16:Y=3
16200 ONKEYGOSUB,,,,,,,23000
16300 IFLD%=1THENGOSUB30000
16500 GOTO2915
17000 POKEVARPTR(XY%(0)),X:POKEVARPTR(XY%(0))+1,Y:CALLVARPTR(XY%(3)),0,VARPTR(XY%(0))
17020 FC%=0:W%=0:R%=0:FORN%=0TO1:FORTY%=PEEK(VARPTR(XY%(2)))TOPEEK(VARPTR(XY%(2))+1):FORTX%=PEEK(VARPTR(XY%(1)))TOPEEK(VARPTR(XY%(1))+1)
17030 IFN%=0ANDS%(TX%,TY%,1)=1THENFC%=FC%+1
17035 IFN%=1ANDS%(TX%,TY%,0)<2ANDS%(TX%,TY%,1)<>1THENST%(W%,0)=TX%:ST%(W%,1)=TY%:W%=W%+1:S%(TX%,TY%,0)=S%(TX%,TY%,0)+2
17040 NEXT:NEXT:IFFC%=S%(X,Y,2)THENNEXTN%ELSE3000
17050 IFW%<>0THEN5060ELSE3000
17100 CLS:GOSUB17200:GOSUB17300:GOSUB18000:RETURN'
17200 PRINT@0*YO%+32,"    TEXT";:PRINT@1*YO%+32," SWEEPER";:RETURN
17300 PRINT@3*YO%+32," SQUARES";:PRINT@4*YO%+32,"   LEFT:";:PRINT@5*YO%+32,"        ";:PRINT@5*YO%+32,"    "+STR$(MC%);:PRINT@7*YO%+32,H$;:RETURN
18000 GOSUB24000:KEY(8)STOP:FORTY%=0TO7:LS$="":FORTX%=0TO31'
18010 IFS%(TX%,TY%,1)=1THENT$=CHR$(255)ELSET$=CHR$(239)
18020 IFS%(TX%,TY%,0)>1THENT$=RIGHT$(STR$(S%(TX%,TY%,2)),1)
18030 IFT$="0"THENT$=" 
18040 LS$=LS$+T$:NEXTTX%:PRINT@YO%*TY%,LS$;:NEXTTY%:KEY(8)ON:RETURN
20000 GOSUB24000:A$=INKEY$'
20010 IFA$=""THENA$="Q
20020 IFA$="Y"ORA$="y"THENYN%=1:RETURN
20030 IFA$="N"ORA$="n"THENYN%=0:RETURN
20040 GOTO20000
21000 IFLD%<>1THENGOSUB30000ELSE3000'
21010 IFPEEK(65343)<>238THENPRINTCHR$(27)+"U":GOSUB30000
21015 A$=INKEY$:IFA$="H"ORA$="h"THEN21020ELSE21010
21020 GOSUB17100:GOTO3000
23000 GOSUB24000:GOSUB31000:KEY(8)STOP:PRINT@3*YO%+35,"Quit?";:PRINT@4*YO%+35,"(y/n)";:GOSUB20000:KEY(8)ON:IFYN%=1THENCLS:MENUELSEGOSUB17200:GOSUB17300:RETURN
24000 PRINTCF$:PRINTCHR$(27)+CHR$(86):RETURN
30000 IFLD%<>1THENCLS
30010 GOSUB24000'
30020 KEY(8)STOP:PRINT@0*YO%+15+PO%,"Controls";
30030 PRINT@1*YO%+PO%,CHR$(235)+STRING$(38,231)+CHR$(236);
30040 PRINT@2*YO%+PO%,CHR$(233)+"Cursor:      [W][A][S][D]/"+"["+CHR$(152)+"]["+CHR$(155)+"]["+CHR$(153)+"]["+CHR$(154)+"]"+CHR$(234);
30050 PRINT@3*YO%+PO%,CHR$(233)+"Check Tile:            [Space]/[Enter]"+CHR$(234);
30060 PRINT@4*YO%+PO%,CHR$(233)+"Set/Unset Flag:                    [F]"+CHR$(234);
30070 IFLD%<>1THENPRINT@5*YO%+PO%,CHR$(233)+"Help Menu / Return:                [H]"+CHR$(234);ELSEPRINT@5*YO%+PO%,CHR$(233);:PRINT@5*YO%+PO%+39,CHR$(234);
30080 PRINT@6*YO%+PO%,CHR$(233)+"Exit to Menu:                     [F8]"+CHR$(234);
30090 PRINT@7*YO%+PO%,CHR$(237)+STRING$(38,232)+CHR$(238);:KEY(8)ON:RETURN
31000 FORTY=0TO7'
31010 PRINT@TY*YO%+32,STRING$(8,32);
31020 NEXT:RETURN
32000 CLS:PRINT@0*YO%+11,"Select Difficulty
32010 PRINT@2*YO%+1,"1. Easy   (30)
32020 PRINT@4*YO%+1,"2. Medium (46)
32030 PRINT@6*YO%+1,"3. Hard   (52)
32040 PRINT@2*YO%+21,"4. Classic (36)
32050 PRINT@4*YO%+21,"5. Custom":RETURN